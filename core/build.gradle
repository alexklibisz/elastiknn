// TODO: split this project into core and api.
// api should just contain the api objects and codecs.
// elastic4s-client and core should both depend on api.
// No reason for the client to have access to the LSH models, etc.

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    runtime "org.scala-lang:scala-library:${scalaVersion}"
    implementation "org.scala-lang:scala-library:${scalaVersion}"
    compile "io.circe:circe-generic_${scalaVersion}:${circeVersion}"
    compile "io.circe:circe-parser_${scalaVersion}:${circeVersion}"
    compile "io.circe:circe-generic-extras_${scalaVersion}:${circeVersion}"
    implementation "com.google.guava:guava:28.1-jre"
    runtime "com.google.guava:guava:28.1-jre"
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "${project.name}_${scalaVersion}"
            from components.java
            artifact tasks.sourceJar
            artifact tasks.javadocJar
            pom {
                name = "${rootProject.group}:${artifactId}"
                description = 'Schema objects and algorithms for the ElastiKnn Elasticsearch plugin'
                url = 'https://github.com/alexklibisz/elastiknn'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'alexklibisz'
                        name = 'Alex Klibisz'
                        email = 'aklibisz@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/alexklibisz/elastiknn.git'
                    developerConnection = 'scm:git:ssh://github.com/alexklibisz/elastiknn.git'
                    url = 'https://github.com/alexklibisz/elastiknn'
                }
            }

        }
    }

    repositories {
        maven {
            url sonatypeUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

}

signing {
    sign publishing.publications
}
